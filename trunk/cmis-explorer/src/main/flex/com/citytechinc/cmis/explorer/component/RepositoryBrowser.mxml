<?xml version="1.0" encoding="utf-8"?>
<mx:VBox xmlns:mx="http://www.adobe.com/2006/mxml" creationComplete="init()" verticalGap="0">

	<mx:Script>
		<![CDATA[
			import com.citytechinc.cmis.client.event.ChildrenEvent;
			import com.citytechinc.cmis.client.event.DocumentEvent;
			import com.citytechinc.cmis.client.model.Folder;
			import com.citytechinc.cmis.client.service.ChildrenService;
			import com.citytechinc.cmis.client.service.ContentStreamService;
			import com.citytechinc.cmis.client.service.DocumentService;
			import com.citytechinc.cmis.explorer.Controller;
			
			import mx.collections.ArrayCollection;
			import mx.events.DragEvent;
			import mx.events.ListEvent;
			import mx.events.TreeEvent;
			
			private var childrenService:ChildrenService;
			
			[Bindable] private var documentService:DocumentService;
			
			[Bindable] private var folders:ArrayCollection;
			
			[Bindable] public var controller:Controller;
			
			private function init():void
			{
				childrenService = new ChildrenService(controller.auth);
				
				childrenService.addEventListener(ChildrenEvent.REPO_CHILDREN, setFolders);
				
				childrenService.getRepositoryChildren(controller.repository);
				
				documentService = new DocumentService(controller.auth);
				
				documentService.addEventListener(DocumentEvent.CREATED, getFolder);
				
				folderTree.addEventListener(ListEvent.ITEM_CLICK, getFolderDocuments);
				folderTree.addEventListener(TreeEvent.ITEM_OPENING, getFolderChildren);
				
				docList.addEventListener(DragEvent.DRAG_START, getDocuments);
				docList.addEventListener(NativeDragEvent.NATIVE_DRAG_ENTER, uploadFiles);
				docList.addEventListener(NativeDragEvent.NATIVE_DRAG_DROP, createDocuments);
			}
			
			private function setFolders(event:ChildrenEvent):void
			{
				folders = event.folders;
			}
			
			private function getFolderDocuments(event:ListEvent):void
			{
				var folder:Folder = Folder(folderTree.selectedItem);
				
				if (!folder.opened)
					childrenService.getFolderChildren(folder);
			}
			
			private function getFolderChildren(event:TreeEvent):void
			{
				var folder:Folder = Folder(event.item);
				
				if (!folder.opened)
					childrenService.getFolderChildren(folder);
			}
			
			private function invalidateTree(event:Event):void
			{
				folderTree.invalidateList();
			}
			
			private function getDocuments(event:DragEvent):void
			{
				var documents:Array = docList.selectedItems;
				
				var contentStreamService:ContentStreamService = new ContentStreamService(controller.auth, documents);
				
				var clip:Clipboard = new Clipboard();

				clip.setDataHandler(ClipboardFormats.FILE_LIST_FORMAT, contentStreamService.getContentStreams);

				var dragOptions:NativeDragOptions = new NativeDragOptions();

				dragOptions.allowCopy = true;
				dragOptions.allowLink = true;
				dragOptions.allowMove = false;

				NativeDragManager.doDrag(event.currentTarget as InteractiveObject, clip, null, null, dragOptions);	
			}
			
			public function uploadFiles(event:NativeDragEvent):void 
			{
				if (event.clipboard.hasFormat(ClipboardFormats.FILE_LIST_FORMAT))
					NativeDragManager.acceptDragDrop(event.currentTarget as InteractiveObject);
			}
			
			public function createDocuments(event:NativeDragEvent):void
			{
				var files:Array = event.clipboard.getData(ClipboardFormats.FILE_LIST_FORMAT) as Array;
				
				documentService.createDocuments(Folder(folderTree.selectedItem), files);
			}
			
			public function getFolder(event:DocumentEvent):void
			{
				var folder:Folder = Folder(folderTree.selectedItem);
				
				folder.opened = false;
				
				childrenService.getFolderChildren(folder);
			}
			
		]]>
	</mx:Script>
	
	<mx:VBox styleName="dataGridBox2" height="2" width="100%" />
	<mx:HDividedBox label="Browser" height="100%" width="100%" backgroundColor="#776441">
		<!-- navigation tree (folders) -->
		<mx:VBox height="100%" width="230" paddingLeft="10" styleName="typesBox">
			<mx:Tree id="folderTree" dataProvider="{folders}" height="100%" width="100%" labelField="name" />
		</mx:VBox>
		<!-- folder contents (files) -->
		<mx:VBox height="100%" width="230" styleName="typesBox">
			<mx:List id="docList" dataProvider="{folderTree.selectedItem.documents}" height="100%" width="100%" labelField="name" allowMultipleSelection="true" dragEnabled="true" />
		</mx:VBox>
		<!-- file properties -->
		<mx:VBox height="100%" verticalGap="0">
			<!-- document form -->
			<mx:Form label="Sign In" styleName="typesBox" width="100%" paddingBottom="10" paddingLeft="10" paddingTop="10">
				<mx:Button id="coBtn" label="check-out" enabled="false" disabledColor="#776441" />
			</mx:Form>
			<!-- document properties -->
			<mx:VBox styleName="dataGrid" height="100%" width="100%">
				<mx:DataGrid id="propList" dataProvider="{docList.selectedItem.properties}" height="100%" width="100%">
					<mx:columns>
						<mx:DataGridColumn dataField="name" headerText="Name" width="200" />
						<mx:DataGridColumn dataField="value" headerText="Value" />
					</mx:columns>
				</mx:DataGrid>
			</mx:VBox>
		</mx:VBox>
	</mx:HDividedBox>
</mx:VBox>